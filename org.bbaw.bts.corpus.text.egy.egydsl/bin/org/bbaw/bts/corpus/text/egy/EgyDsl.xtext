grammar org.bbaw.bts.corpus.text.egy.EgyDsl
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate egyDsl "http://www.bbaw.org/bts/corpus/text/egy/EgyDsl"


    
    
TextContent:
	{TextContent}
	(items+=TextItem ((' ' | NEWLINE)?  (items+=TextItem))*);


TextItem:
	Sentence;

// sentence	
Sentence:
	{Sentence}
	'§' (items+=SentenceItem (' ' (NEWLINE)? items+=SentenceItem)*)? '§';

SentenceItem:
	 Word | AbstractMarker |  Ambivalence
;

AbstractMarker:
	Marker | 
	VersMarker |
	DestructionMarker
;

terminal NEWLINE returns ecore::EString :
// New line on DOS or Unix 
    ('\r'|'\n'|'\t')+;

Ambivalence:
	'%' (cases+=Case ('| ' (NEWLINE)? cases+=Case)+) '%'
;

Case:
	name=CASESTRING (NEWLINE)? (items+=SentenceItemNoAmbivalence (' ' (NEWLINE)? items+=SentenceItemNoAmbivalence)*) 
;

terminal CASESTRING returns ecore::EString:
	'case ' (!(':'))+ ': '
;

SentenceItemNoAmbivalence:
	Word | AbstractMarker
;



Marker:
	type = BETWEEN_HASHES
;

terminal BETWEEN_HASHES  returns ecore::EString:
	'#' (!'#')+ '#'
;

DestructionMarker:
	type=BETWEEN_MINUS
;
terminal BETWEEN_MINUS  returns ecore::EString:
	'--' (!'-')+ '--'
;


// word
Word:
	{Word}
	
	( wChar+=WordPart+)	//('-') (wChar+=WordPart)*
;

WordPart:
	( wChar= WordMiddle )// (wChar+=WordMiddle)*(wChar+=WordEnding)? (wChar+=WordEnding)?)	
;


WordMiddle:
//	{WordMiddle}
	Brackets | Chars | Interfix
;

// Chars...
Chars:
	name=EGYSTRING
;


terminal EGYSTRING returns ecore::EString:(
 // AlephU
 '\uA722'
 //&aleph;=
 |'\uA723'
 |'\u02BE'   // spiLen

//|'\u02BE'   // SpiLenU
|'\uA725'   // ajin
|'\uA724'   // AjinU
 
 |'j'|'y'|'w'|'b'|'f'|'m'|'n'|'r'
 |'h'|'ḥ'|'ḫ'|'ẖ'|'H'|'Ḥ'|'Ḫ'|'H̱'|'H̭'
 |'z'
 |'s'|'š'|'S'|'Š'   // Schekelsche Zeichen|'Ś'|'ś'
 |'q'|'k'|'g'
 |'t'|'ṯ'|'ṱ'|'T'|'Ṯ'|'Ṱ'
 // Schekelsche Zeichen |'ṭ' |'Ṭ'
 |'d'|'ḏ'|'Ḏ'
 |'h̭'
 |'i'|'i̯'|'ı͗'|'ı̯͗'|'ï'|'i̭'|'I͗'|'I'
// |'\u0069\u032F'//iArcU
 |'\u0049\u032F' //IArcU
 |'i̯̯'
 |'\u00CF'   // ITremaU
 // Schekelsche Zeichen |'ḳ'|'Ḳ'
 |'u'|'u̯'|'U'|'\u0055\u032F'
 
 |'e'|'/'|'+'
 
 // Schekelsche Zeichen|'č'|'č̣'|'Č'|'Č̣'
 |'a'|'J'|'Y'|'W'|'B'|'P'|'F'|'M'
 |'N'|'R'|'L'
 
 |'Z'|'Q'|'K'|'G'|'D'
 |'A'
 

// coptic
	|'\u2C80'..'\u2CFF'
//	//oval
//	| '\uD80C\uDE86' |'\uD80C\uDE87'
//	
//	//cartouche
//	|'\uD80C\uDF79' |'\uD80C\uDF7A'
//	|'\u13379'   // cartOn
//	|'\u1337A'   // cartOff
//	//serech
//	|'\uD80C\uDE58' | '\uD80C\uDE82'
	|'p'|'l' 
	
	//interfix :
	|'='
//	|':'|'='|'\u2261'|','|'.'|'~'|'\u22ee'
	|'_'
	|'\u205D'   // trplColon	
	//num
	|'0'..'9'
	
	// special
	|'Õ'|'ã'|'Þ'|'!'
	|'\u0152'   // OElig
	|'\u0153'   // oelig
	|'\u0178'   // Yuml
	|'\u00C6'
	|'*'|'ê'
)+;

// textual criticism brackets
Brackets:
	Rasur | AncientExpanded | RestorationOverRasur | ExpandedColumn |
	Expanded  | DisputableReading | Emendation | Lacuna | Deletion | 
	PartialDestruction
	|Cartouche |Oval |Serech
;

//Cartouche2:
//	'\u13379'   wChar+=NoCartouche+ '\u1337A'
//;

Oval: //Oval
	'\uD80C\uDE58' wChar+=NoCartouche+ '\uD80C\uDE82'
;

Serech:// eigentlich Serech
	'\uD80C\uDF79' wChar+=NoCartouche+ '\uD80C\uDF7A'
;
Cartouche:
	{Expanded}
	'\uD80C\uDE86'  wChar+=NoCartouche+ '\uD80C\uDE87'
;

NoCartouche:
	Chars 
	| Rasur | AncientExpanded | RestorationOverRasur | ExpandedColumn |
	Expanded  | DisputableReading | Emendation | Lacuna | Deletion | 
	PartialDestruction  
	| Interfix 
;

Expanded:
	{Expanded}
	'(' wChar+=NoExpanded+ ')'
;

AncientExpanded:
	{AncientExpanded}
	'((' wChar+=NoAncientExpanded+ '))'
;
NoExpanded:
	DisputableReading |Chars | Interfix
;

Emendation:
	{Emendation}
	'\u2329' wChar+=NoEmendation+ '\u232A'
	;

NoEmendation:
	Expanded | DisputableReading |Chars | Interfix
;

DisputableReading:
	{DisputableReading}
	'\u2E2E' wChar+=NoDisputableReading+ '?'
;

NoDisputableReading:
	 Expanded | Emendation | Deletion 
	 | Rasur | AncientExpanded | RestorationOverRasur | ExpandedColumn
	 | Lacuna 
	 | PartialDestruction
	 | Chars | Interfix
;

Lacuna:
	{Lacuna}
	'[' wChar+=NoLacuna+ ']'
;

NoLacuna: 
	Expanded | DisputableReading |Cartouche |Oval |Serech | Deletion | Emendation |Chars | Interfix
;

Deletion:
	{Deletion}
	'{' wChar+=NoDeletion+ '}'
;

NoDeletion:
	PartialDestruction | Expanded | DisputableReading | Lacuna | RestorationOverRasur |AncientExpanded |Chars | Interfix
;

ExpandedColumn:
	{ExpandedColumn}
	'\u2329\u2329' wChar+=NoExpandedColumn+ '\u232A\u232A'
;

NoExpandedColumn:
	Expanded | DisputableReading | Emendation | Lacuna | PartialDestruction | Deletion |Chars | Interfix 
;

Rasur:
	{Rasur}
	'{{' wChar+=NoRasur+ '}}'
;

NoRasur:
	Expanded | DisputableReading | Emendation | Lacuna | PartialDestruction | Deletion|Chars | Interfix 
;



NoAncientExpanded:
	Expanded | DisputableReading | Emendation | Lacuna | PartialDestruction | Deletion |Chars | Interfix 
;

RestorationOverRasur:
	{RestorationOverRasur}
	'[[' wChar+=NoRestorationOverRasur+ ']]'
;

NoRestorationOverRasur:
	Expanded | DisputableReading | Cartouche | Emendation | Lacuna | PartialDestruction | Deletion|Chars | Interfix 
;

PartialDestruction:
	{PartialDestruction}
	'\u2E22' wChar+=NoPartialDestruction+ '\u2E23'
;

NoPartialDestruction:
	Deletion | Expanded | DisputableReading |Cartouche |Oval |Serech | Emendation|Chars | Interfix
;

// textual criticism interfixes
Interfix:
	InterfixFlexion | InterfixLexical |
	 InterfixSuffixPronomLexical |InterfixPrefixNonLexical |InterfixPrefixLexical
	 | InterfixConnectionSyllabicGroup
	 | InterfixCompoundWords
	 |InterfixPhoneticalComplement
;

InterfixLexical:
	{InterfixLexical}
	','
;

InterfixFlexion:
	{InterfixFlexion}
	'.'
;

InterfixSuffixPronomLexical:
	{InterfixSuffixPronomLexical}
	'\u2261'
;

InterfixPrefixNonLexical:
	{InterfixPrefixNonLexical}
	':'
;

InterfixPrefixLexical:
	{InterfixPrefixLexical}
	'\u22ee'
;

InterfixConnectionSyllabicGroup:
	{InterfixConnectionSyllabicGroup}
	'~'
;

InterfixCompoundWords:
	{InterfixCompoundWords}
	'-'
;

InterfixPhoneticalComplement:
	{InterfixPhoneticalComplement}
	';'
;

VersMarker:
	VersbreakMarker | VersFrontierMarker |BrokenVersbreakMarker 
	| MissingVersMarker | DestroyedVersMarker | DeletedVersMarker | DisputableVersMarker
	| RestorationOverRasurMarker | AncientExpandedMarker | RasurMarker 
	|EmendationVersMarker |DestroyedVersFrontierMarker | PartialDestroyedVersMarker | PartialDestroyedDisputableVersMarker
	| DestroyedDisputableVersFrontierMarker | DisputableDestroyedVersMarker |DeletedDisputableVersMarker
	| MissingDisputableVersMarker | DisputableDeletedVersMarker | PartialDestroyedDeletedVersMarker
	| DestroyedDeletedVersMarker | DeletedDestroyedVersMarker
;

EmendationVersMarker:
{EmendationVersMarker}
	'(\uDB80\uDC80)'//'@v'
;

DisputableVersMarker:
{DisputableVersMarker}
	'\u2E2E\uDB80\uDC80?'//'@v'
;

DisputableDestroyedVersMarker:
{DisputableDestroyedVersMarker}
	'\u2E2E[\uDB80\uDC80]?'//'@v'
;

DisputableDeletedVersMarker:
{DisputableDeletedVersMarker}
	'\u2E2E{\uDB80\uDC80}?'//'@v'
;

DeletedVersMarker:
{DeletedVersMarker}
	'{\uDB80\uDC80}'//'@v'
;

DeletedDisputableVersMarker:
{DeletedDisputableVersMarker}
	'{\u2E2E\uDB80\uDC80?}'//'@v'
;

DeletedDestroyedVersMarker:
{DeletedDestroyedVersMarker}
	'{[\uDB80\uDC80]}'//'@v'
;

DestroyedVersMarker:
	{DestroyedVersMarker}
	'[\uDB80\uDC80]'//'@v'
;

DestroyedVersFrontierMarker:
	{DestroyedVersFrontierMarker}
	'[\uDB80\uDC81]'//'@v'
;

DestroyedDisputableVersFrontierMarker:
	{DestroyedDisputableVersFrontierMarker}
	'[\u2E2E\uDB80\uDC81?]'//'@v'
;

DestroyedDeletedVersMarker:
	{DestroyedDeletedVersMarker}
	'[{\uDB80\uDC80}]'//'@v'
;


PartialDestroyedVersMarker:
	{PartialDestroyedVersMarker}
	'\u2E22\uDB80\uDC80\u2E23'//'@v'
;

PartialDestroyedDisputableVersMarker:
	{PartialDestroyedDisputableVersMarker}
	'\u2E22\u2E2E\uDB80\uDC80?\u2E23'//'@v'
;

PartialDestroyedDeletedVersMarker:
	{PartialDestroyedDeletedVersMarker}
	'\u2E22{\uDB80\uDC80}\u2E23'//'@v'
;

MissingVersMarker:
	{MissingVersMarker}
	'\u2329\uDB80\uDC80\u232A'//'@v'
;

MissingDisputableVersMarker:
	{MissingDisputableVersMarker}
	'\u2329\u2E2E\uDB80\uDC80?\u232A'//'@v'
;

RestorationOverRasurMarker:
	{RestorationOverRasurMarker}
	'[[\uDB80\uDC80]]'//'@v'
;

AncientExpandedMarker:
	{AncientExpandedMarker}
	'((\uDB80\uDC80))'//'@v'
;

RasurMarker:
	{AncientExpandedMarker}
	'{{\uDB80\uDC80}}'//'@v'
;

VersFrontierMarker:
	{VersFrontierMarker}
//	'\uF0081'
	'\uDB80\uDC81'//'@mv'
;

VersbreakMarker:
	{VersbreakMarker}
//	'\uF0080'
	'\uDB80\uDC80'//'@v'
;

BrokenVersbreakMarker:
	{BrokenVersbreakMarker}
	'\uDB80\uDC82'//'@v'
;